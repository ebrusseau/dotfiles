#
# .zshrc: Execute commands at the start of an interactive session.
#

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.config/zsh/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Lazy-load (autoload) Zsh function files
ZFUNCDIR=${ZDOTDIR:-$HOME}/zfunctions
fpath=($ZFUNCDIR $fpath)
autoload -Uz $ZFUNCDIR/*(.:t)

# Set any zstyles to set configurations prior to loading plugins
[[ ! -f ${ZDOTDIR:-$HOME}/.zstyles ]] || source ${ZDOTDIR:-$HOME}/.zstyles

# Install antidote
if [[ "$(command -v brew)" ]]; then
  HOMEBREW_PATH=$(dirname $(which brew))
  HOMEBREW_PREFIX="${HOMEBREW_PATH%/*}"
  ANTIDOTE_PATH=${HOMEBREW_PREFIX}/share/antidote/antidote.zsh
else
  ANTIDOTE_PATH=${XDG_DATA_HOME}/antidote/antidote.zsh
  if [[ ! -f $ANTIDOTE_PATH ]]; then
    git clone --depth 1 https://github.com/mattmc3/antidote "$(dirname "$ANTIDOTE_PATH")"
  fi
fi

# Load antidote
if [[ -f $ANTIDOTE_PATH ]]; then
  source "$ANTIDOTE_PATH"
  antidote load
else 
  echo Could not find antidote.
fi

# Source anything in .zshrc.d.
for _rc in ${ZDOTDIR:-$HOME}/zshrc.d/*.zsh; do
  # Ignore tilde files.
  if [[ $_rc:t != '~'* ]]; then
    source "$_rc"
  fi
done
unset _rc

# To customize prompt, run `p10k configure` or edit .p10k.zsh.
[[ ! -f ${ZDOTDIR:-$HOME}/.p10k.zsh ]] || source ${ZDOTDIR:-$HOME}/.p10k.zsh